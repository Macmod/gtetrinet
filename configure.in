dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_PREREQ(2.52)
AC_CONFIG_SRCDIR([src/gtetrinet.c])
AM_CONFIG_HEADER(config.h:config.h.in)
AM_INIT_AUTOMAKE(gtetrinet, 0.5.2)

AC_DEFINE(GETTEXT_PACKAGE, "gtetrinet", [Gettext domain name])
GETTEXT_PACKAGE=gtetrinet
AC_SUBST(GETTEXT_PACKAGE)

AC_PROG_CC

AC_PROG_INTLTOOL([0.23])
ALL_LINGUAS="ca de sv"
AM_GNU_GETTEXT_VERSION([0.11.5])
AM_GLIB_GNU_GETTEXT

dnl *****************************************
dnl pkg-config check time
dnl *****************************************

LIBGNOME_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0

dnl *****************************************
dnl libgnome, libgnomeui needed for all utils
dnl *****************************************

PKG_CHECK_MODULES(GTET, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)
AC_SUBST(GTET_CFLAGS)
AC_SUBST(GTET_LIBS)

AM_PATH_ESD(0.2.5, AC_DEFINE(HAVE_ESD, 1, [Define this to enable EsounD support.]))

AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(sys/time.h unistd.h)

AC_CHECK_FUNC(putenv)
AC_CHECK_FUNC(setenv)
if test $ac_cv_func_setenv = no; then
  if test $ac_cv_func_putenv = no; then
    echo "Error: Couldn't find either putenv() or setenv()"
    exit;
  else
    AC_DEFINE(HAVE_PUTENV, 1, [Define this if putenv() is available.])
  fi
 else
  AC_DEFINE(HAVE_SETENV, 1, [Define this if setenv() is available.])
 fi

dnl configure options
AC_ARG_ENABLE(detach,
[  --enable-detach         Enable page detaching (default=no)],
  [if test "x$enable_detach" = "xyes"; then
    AC_DEFINE(ENABLE_DETACH, 1, [Define this to enable page detaching.])
  fi])

AC_ARG_ENABLE(ipv6,
[  --enable-ipv6           Enable IPv6 support (default=no)],
  [if test "x$enable_ipv6" = "xyes"; then
   AC_DEFINE(USE_IPV6, 1, [Define this to enable IPv6 support.])
  fi])

AM_GNU_GETTEXT([external])

AC_OUTPUT([
Makefile
doc/Makefile
po/Makefile.in
src/Makefile
src/images/Makefile
themes/Makefile
themes/default/Makefile
themes/small/Makefile
])
